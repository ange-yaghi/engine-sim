import "engine_sim.mr"

units units()
constants constants()
impulse_response_library ir_lib()

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

label cycle(2 * 360 * units.deg)
public node wb_ignition {
    input wires;
    input timing_curve;
    input rev_limit: 7500 * units.rpm;
    alias output __out:
        ignition_module(
            timing_curve: timing_curve,
            rev_limit: rev_limit,
            limiter_duration: 0.1
        )
            .connect_wire(wires.wire1, (0.0 / 6.0) * cycle)
            .connect_wire(wires.wire5, (1.0 / 6.0) * cycle)
            .connect_wire(wires.wire3, (2.0 / 6.0) * cycle)
            .connect_wire(wires.wire6, (3.0 / 6.0) * cycle)
            .connect_wire(wires.wire2, (4.0 / 6.0) * cycle)
            .connect_wire(wires.wire4, (5.0 / 6.0) * cycle);
}

public node t2jz_camshaft_builder {
    input lobe_profile: comp_cams_magnum_11_450_8_lobe_profile();
    input intake_lobe_profile: lobe_profile;
    input exhaust_lobe_profile: lobe_profile;
    input lobe_separation: 110.0 * units.deg;
    input intake_lobe_center: lobe_separation;
    input exhaust_lobe_center: lobe_separation;
    input advance: 0.0 * units.deg;
    input base_radius: 0.75 * units.inch;

    output intake_cam: _intake_cam;
    output exhaust_cam: _exhaust_cam;

    camshaft_parameters params(
        advance: advance,
        base_radius: base_radius
    )

    camshaft _intake_cam(params, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam(params, lobe_profile: exhaust_lobe_profile)

    label rot(2 * (360 / 6.0) * units.deg)
    label rot360(360 * units.deg)

    // 1 5 3 6 2 4
    _exhaust_cam
        .add_lobe((rot360 - exhaust_lobe_center) + 0 * rot)
        .add_lobe((rot360 - exhaust_lobe_center) + 4 * rot)
        .add_lobe((rot360 - exhaust_lobe_center) + 2 * rot)
        .add_lobe((rot360 - exhaust_lobe_center) + 5 * rot)
        .add_lobe((rot360 - exhaust_lobe_center) + 1 * rot)
        .add_lobe((rot360 - exhaust_lobe_center) + 3 * rot)

    _intake_cam
        .add_lobe(rot360 + intake_lobe_center + 0 * rot)
        .add_lobe(rot360 + intake_lobe_center + 4 * rot)
        .add_lobe(rot360 + intake_lobe_center + 2 * rot)
        .add_lobe(rot360 + intake_lobe_center + 5 * rot)
        .add_lobe(rot360 + intake_lobe_center + 1 * rot)
        .add_lobe(rot360 + intake_lobe_center + 3 * rot)
}

private node t2jz_head {
    input intake_camshaft;
    input exhaust_camshaft;
    input chamber_volume: 50 * units.cc;
    input intake_runner_volume: 149.6 * units.cc;
    input intake_runner_cross_section_area: 1.9 * units.inch * 1.9 * units.inch;
    input exhaust_runner_volume: 50.0 * units.cc;
    input exhaust_runner_cross_section_area: 1.25 * units.inch * 1.25 * units.inch;

    input flow_attenuation: 1.0;
    input lift_scale: 1.0;
    input flip_display: false;
    alias output __out: head;

    function intake_flow(50 * units.thou)
    intake_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(50 * lift_scale, 58 * flow_attenuation)
        .add_flow_sample(100 * lift_scale, 103 * flow_attenuation)
        .add_flow_sample(150 * lift_scale, 156 * flow_attenuation)
        .add_flow_sample(200 * lift_scale, 214 * flow_attenuation)
        .add_flow_sample(250 * lift_scale, 249 * flow_attenuation)
        .add_flow_sample(300 * lift_scale, 268 * flow_attenuation)
        .add_flow_sample(350 * lift_scale, 280 * flow_attenuation)
        .add_flow_sample(400 * lift_scale, 280 * flow_attenuation)
        .add_flow_sample(450 * lift_scale, 281 * flow_attenuation)

    function exhaust_flow(50 * units.thou)
    exhaust_flow
        .add_flow_sample(0 * lift_scale, 0 * flow_attenuation)
        .add_flow_sample(50 * lift_scale, 37 * flow_attenuation)
        .add_flow_sample(100 * lift_scale, 72 * flow_attenuation)
        .add_flow_sample(150 * lift_scale, 113 * flow_attenuation)
        .add_flow_sample(200 * lift_scale, 160 * flow_attenuation)
        .add_flow_sample(250 * lift_scale, 196 * flow_attenuation)
        .add_flow_sample(300 * lift_scale, 222 * flow_attenuation)
        .add_flow_sample(350 * lift_scale, 235 * flow_attenuation)
        .add_flow_sample(400 * lift_scale, 245 * flow_attenuation)
        .add_flow_sample(450 * lift_scale, 246 * flow_attenuation)

    generic_cylinder_head head(
        chamber_volume: chamber_volume,
        intake_runner_volume: intake_runner_volume,
        intake_runner_cross_section_area: intake_runner_cross_section_area,
        exhaust_runner_volume: exhaust_runner_volume,
        exhaust_runner_cross_section_area: exhaust_runner_cross_section_area,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        valvetrain: standard_valvetrain(
            intake_camshaft: intake_camshaft,
            exhaust_camshaft: exhaust_camshaft
        ),
        flip_display: flip_display
    )
}

public node t2jz {
    alias output __out: engine;

    wires wires()

    engine engine(
        name: "2JZ [I6]",
        starter_torque: 200 * units.lb_ft,
        redline: 6000 * units.rpm,
        fuel: fuel(
            max_burning_efficiency: 1.0
        ),
        hf_gain: 0.01,
        noise: 1.0,
        jitter: 0.23,
        simulation_frequency: 10000
    )

    label stroke(86.0 * units.mm)
    label bore(86.0 * units.mm)
    label rod_length(142 * units.mm)
    label rod_mass(500 * units.g)
    label compression_height(32.8 * units.mm)
    label crank_mass(15 * units.kg)
    label flywheel_mass(10 * units.kg)
    label flywheel_radius(7 * units.inch)

    label crank_moment(
        disk_moment_of_inertia(mass: crank_mass, radius: stroke / 2)
    )
    label flywheel_moment(
        disk_moment_of_inertia(mass: flywheel_mass, radius: flywheel_radius)
    )
    label other_moment( // Moment from cams, pulleys, etc [estimated]
        disk_moment_of_inertia(mass: 20 * units.kg, radius: 8.0 * units.cm)
    )

    crankshaft c0(
        throw: stroke / 2,
        flywheel_mass: flywheel_mass,
        mass: crank_mass,
        friction_torque: 5.0 * units.lb_ft,
        moment_of_inertia:
            crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: constants.pi / 2
    )

    // 1 5 3 6 2 4
    rod_journal rj0(angle: 0 * units.deg)
	rod_journal rj1(angle: 480 * units.deg)
	rod_journal rj2(angle: 240 * units.deg)
	rod_journal rj3(angle: 600 * units.deg)
    rod_journal rj4(angle: 120 * units.deg)
    rod_journal rj5(angle: 360 * units.deg)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)

    piston_parameters piston_params(
        mass: (200 + 50) * units.g,
        blowby: 0,
        compression_height: compression_height,
        wrist_pin_position: 0 * units.mm,
        displacement: 0.0
    )

    connecting_rod_parameters cr_params(
        mass: rod_mass,
        moment_of_inertia: rod_moment_of_inertia(
            mass: rod_mass,
            length: rod_length
        ),
        center_of_mass: 0.0,
        length: rod_length
    )

    cylinder_bank_parameters bank_params(
        bore: bore,
        deck_height: stroke / 2 + rod_length + compression_height
    )

    intake intake(
        plenum_volume: 1.0 * units.L,
        plenum_cross_section_area: 10.0 * units.cm2,
        intake_flow_rate: k_carb(500.0),
        runner_flow_rate: k_carb(200.0),
        runner_length: 40.0 * units.inch,
        idle_flow_rate: k_carb(0.0),
        idle_throttle_plate_position: 0.9965
    )

    exhaust_system_parameters es_params(
        outlet_flow_rate: k_carb(1000.0),
        primary_tube_length: 40.0 * units.inch,
        primary_flow_rate: k_carb(400.0),
        velocity_decay: 1.0
    )
    
    exhaust_system exhaust0(
        es_params,
        length: 100.0 * units.inch,
        audio_volume: 0.2,
        impulse_response: ir_lib.mild_exhaust_0_reverb)
    exhaust_system exhaust1(
        es_params,
        length: 100.0 * units.inch,
        audio_volume: 0.2,
        impulse_response: ir_lib.mild_exhaust_0_reverb)

    label spacing(0.5 * units.inch)

    cylinder_bank b0(bank_params, angle: 0)
    b0
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire1,
            primary_length: spacing * 5,
            sound_attenuation: 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire2,
            primary_length: spacing * 4,
            sound_attenuation: 0.95
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust0,
            ignition_wire: wires.wire3,
            primary_length: spacing * 3,
            sound_attenuation: 0.9
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust1,
            ignition_wire: wires.wire4,
            primary_length: spacing * 3,
            sound_attenuation: 0.97
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.1)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            intake: intake,
            exhaust_system: exhaust1,
            ignition_wire: wires.wire5,
            primary_length: spacing * 4,
            sound_attenuation: 0.98
        )
        .add_cylinder(
            piston: piston(piston_params, blowby: k_28inH2O(0.05)),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            intake: intake,
            exhaust_system: exhaust1,
            ignition_wire: wires.wire6,
            primary_length: spacing * 5,
            sound_attenuation: 0.93
        )

    engine
        .add_cylinder_bank(b0)

    engine.add_crankshaft(c0)

    harmonic_cam_lobe intake_lobe(
        duration_at_50_thou: 220 * units.deg,
        gamma: 1.1,
        lift: 9.78 * units.mm,
        steps: 100
    )

    harmonic_cam_lobe exhaust_lobe(
        duration_at_50_thou: 220 * units.deg,
        gamma: 1.1,
        lift: 9.60 * units.mm,
        steps: 100
    )

    t2jz_camshaft_builder camshaft(
        lobe_profile: "N/A",

        intake_lobe_profile: intake_lobe,
        exhaust_lobe_profile: exhaust_lobe,
        intake_lobe_center: 116 * units.deg,
        exhaust_lobe_center: 116 * units.deg,
        base_radius: (34.0 / 2) * units.mm
    )

    b0.set_cylinder_head (
        t2jz_head(
            chamber_volume: 50 * units.cc,
            intake_camshaft: camshaft.intake_cam,
            exhaust_camshaft: camshaft.exhaust_cam,
            flow_attenuation: 0.9
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 12 * units.deg)
        .add_sample(1000 * units.rpm, 12 * units.deg)
        .add_sample(2000 * units.rpm, 20 * units.deg)
        .add_sample(3000 * units.rpm, 26 * units.deg)
        .add_sample(4000 * units.rpm, 30 * units.deg)
        .add_sample(5000 * units.rpm, 34 * units.deg)
        .add_sample(6000 * units.rpm, 38 * units.deg)
        .add_sample(7000 * units.rpm, 38 * units.deg)

    engine.add_ignition_module(
        wb_ignition(
            wires: wires,
            timing_curve: timing_curve,
            rev_limit: 6500 * units.rpm
        )
    )
}

private node supra_vehicle {
    alias output __out:
        vehicle(
            mass: 3400 * units.lb,
            drag_coefficient: 0.4,
            cross_sectional_area: (66 * units.inch) * (50 * units.inch),
            diff_ratio: 3.15,
            tire_radius: 10 * units.inch,
            rolling_resistance: 500 * units.N
        );
}

private node supra_transmission {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.lb_ft
        )
        .add_gear(5.25)
        .add_gear(3.36)
        .add_gear(2.17)
        .add_gear(1.72)
        .add_gear(1.32)
        .add_gear(1.0);
}

public node main {
    set_engine(t2jz())
    set_vehicle(supra_vehicle())
    set_transmission(supra_transmission())
}
